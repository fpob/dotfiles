# XDG dirs -----------------------------------------------------------------{{{1

ZSH_CONFIG_DIR="${XDG_CONFIG_HOME:-$HOME/.config}/zsh"
ZSH_DATA_DIR="${XDG_DATA_HOME:-$HOME/.local/share}/zsh"
ZSH_STATE_DIR="${XDG_STATE_HOME:-$HOME/.local/state}/zsh"
ZSH_CACHE_DIR="${XDG_CACHE_HOME:-$HOME/.cache}/zsh"

mkdir -p "$ZSH_DATA_DIR" "$ZSH_STATE_DIR" "$ZSH_CACHE_DIR"

# Tmux ---------------------------------------------------------------------{{{1

if [[ $- == *i* && -n $SSH_CONNECTION && -z $TMUX && -z $MC_SID ]] ; then
    # If session "main" exists then attach otherwise create
    tmux -2 new -A -s main
fi

# Oh My Zsh ----------------------------------------------------------------{{{1

# OMZ paths
export ZSH="${XDG_DATA_HOME:-$HOME/.local/share}/oh-my-zsh"
export ZSH_CUSTOM="${XDG_CONFIG_HOME:-$HOME/.config}/oh-my-zsh"

if [[ -n $MC_SID || $TERM = 'linux' ]] ; then
    ZSH_THEME="simple"
else
    ZSH_THEME="powerlevel10k/powerlevel10k"
fi

ZSH_COMPDUMP="$ZSH_CACHE_DIR/zcompdump-$ZSH_VERSION"

# https://github.com/ohmyzsh/ohmyzsh/issues/10972
# https://www.zsh.org/mla/workers/2022/msg00652.html
CASE_SENSITIVE="true"

DISABLE_AUTO_TITLE="true"
ENABLE_CORRECTION="false"
COMPLETION_WAITING_DOTS="true"
ZLE_REMOVE_SUFFIX_CHARS=""
DISABLE_MAGIC_FUNCTIONS="true"

zstyle ':omz:update' mode disabled

# https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters/main.md
typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[path]='none'
ZSH_HIGHLIGHT_STYLES[commandseparator]='fg=245'
ZSH_HIGHLIGHT_STYLES[redirection]='fg=245'
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=42'
ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=42'

zstyle ':grc:ls' disable yes

plugins=(
    asdf
    colored-man-pages
    direnv
    fd
    fzf
    golang
    grc
    kitty
    kubectl
    pip
    python
    ranger
    ripgrep
    sudo
    zoxide
    zsh-autosuggestions
    zsh-completions
    zsh-syntax-highlighting
)

source $ZSH/oh-my-zsh.sh

# Remove all aliases from OMZ plugins
unalias -m '*'

# reload completion functions
autoload -Uz compinit && compinit -d "$ZSH_COMPDUMP"

# Terminal title -----------------------------------------------------------{{{1

# Inspired by oh-my-zsh/lib/termsupport.zsh
function term_title_precmd {
    # %3~ = 3 trailing components of path
    local tab_title="zsh:%15<..<%3~%<<"     # 15 char left truncated PWD
    local win_title="zsh:%3~"               # 3 elements of path
    title $tab_title $win_title
}

# Run before showing the prompt
autoload -U add-zsh-hook
add-zsh-hook precmd term_title_precmd

# History setup ------------------------------------------------------------{{{1
# Has to be done after OMZ setup

export HISTFILE="$ZSH_STATE_DIR/history"
export HISTSIZE=25000
export SAVEHIST=$HISTSIZE

# Don't record an entry starting with a space.
setopt HIST_IGNORE_SPACE
# Delete old recorded entry if new entry is a duplicate.
setopt HIST_IGNORE_ALL_DUPS
# Don't write duplicate entries in the history file.
setopt HIST_SAVE_NO_DUPS

# Variables ----------------------------------------------------------------{{{1

export EDITOR=${EDITOR:-vim}
export PAGER=${PAGER:-less}

if [[ -n "$DISPLAY" ]] ; then
    export BROWSER=${BROWSER:-firefox}
else
    export BROWSER=${BROWSER:-lynx}
fi

# Python REPL init script
export PYTHONSTARTUP="${XDG_CONFIG_HOME:-$HOME/.config}/python/startup.py"
[[ ! -f "$PYTHONSTARTUP" ]] && unset PYTHONSTARTUP

# Python libraries and tools
export REQUESTS_CA_BUNDLE=/etc/ssl/certs/ca-certificates.crt
export PIP_REQUIRE_VIRTUALENV=true
export PIPX_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/pipx"

# golang
export GOPATH="${XDG_DATA_HOME:-$HOME/.local/share}/go"
export GO111MODULE=on

# rust
export CARGO_HOME="${XDG_DATA_HOME:-$HOME/.local/share}/cargo"

# docker XDG dirs
export DOCKER_CONFIG="${XDG_CONFIG_HOME:-$HOME/.config}/docker"

# pspg XDG dirs
export PSPG_CONF="${XDG_CONFIG_HOME:-$HOME/.config}/pspg/config"
export PSPG_HISTORY="${XDG_STATE_HOME:-$HOME/.local/state}/pspg/history"

# Default less options
export LESS=-RK
export LESSHISTFILE="${XDG_STATE_HOME:-$HOME/.local/state}/less/history"

# wget XDG dirs
export WGETRC="${XDG_CONFIG_HOME:-$HOME/.config}/wget/wgetrc"

# https://stackoverflow.com/questions/51504367
export GPG_TTY=$(tty)

# Change jq colors, see `man jq`
export JQ_COLORS='1;31:1;35:1;35:0;39:0;32:1;39:1;39'

export BAT_THEME=base16

export FZF_DEFAULT_COMMAND='fdfind --type f --hidden --follow --exclude .git'

# Aliases ------------------------------------------------------------------{{{1

# "Rename" back commands that are renamed in Debian packages because of
# conflicts with some other commands.
if ( source /etc/os-release && [[ $ID == debian ]] ) ; then
    alias bat=batcat
    alias fd=fdfind
fi

# https://askubuntu.com/questions/22037/aliases-not-available-when-using-sudo
alias sudo='sudo '

alias :q='exit'

alias e='$EDITOR'
alias f='ranger'
alias g='git'

alias xo='xdg-open'

alias cat='bat -p'

alias ls='ls -v --color=auto --hyperlink=auto -CF'
alias l='ls'
alias ll='l -hl --time-style="+%Y-%m-%d %H:%M"'
alias la='l -A'
alias lla='ll -A'

# COW copy if available
alias cp='cp --reflink=auto'

alias rcp='rsync -hhh --info=progress2'

# Human readable sizes
alias du='du -khc'
alias df='df -kTh'
alias free='free -th'

alias rg='rg -S'
alias rgl='rg -l'

# force 256 colors
alias tmux='tmux -2'

alias ci='clipcopy'
alias co='clippaste'

alias bc='bc -ql'

# GPG with CLI pinentry mode in a terminal
alias gpg='gpg --pinentry-mode loopback'

alias t='todo-txt'

alias pa='pueue add --'

alias py='python3'
alias pydoc='pydoc3'
alias ipy='ipython3 --pdb --'   # start debugger on uncaught exception
alias pip='noglob pip'          # allows square brackets for pip command
alias pip3='noglob pip3'

alias a='ansible'
alias ap='ansible-playbook'
alias av='ansible-vault'
alias ad='ansible-doc'
alias adl='ansible-doc -t lookup'
alias ag='ansible-galaxy'

# Extra aliasses in Kitty terminal
if [[ $TERM = xterm-kitty ]] ; then
    # SSH kitten is just wrapper around ssh command that fixes terminfo.
    # *Required* because kitty is not in ncurses database, see discussion on
    # https://github.com/kovidgoyal/kitty/issues/879
    #alias ssh='kitty +kitten ssh'
    alias ssh='TERM=xterm-256color ssh'
    # To fix 'vagrant ssh'
    alias vagrant='TERM=xterm-256color vagrant'
fi

# Load extra scripts -------------------------------------------------------{{{1

function {
    local config_file
    for config_file ("$ZSH_CONFIG_DIR"/*.zsh(N)) ; do
        source $config_file
    done
}
